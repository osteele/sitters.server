#!/usr/bin/env coffee

_ = require 'underscore'
Q = require 'q'
Q.longStackSupport = true
_(global).extend require('../app/lib/models')

SitterProfileData = require '../data/sitters.json'

updateSittersP = Q.all SitterProfileData.map (sitterData) ->
  sitterProfileId = sitterData.id

  # make a copy of the
  profileData = _.extend {}, sitterData
  delete profileData.id

  userAttributes =
    displayName: profileData.name
    email: "sitter-#{sitterProfileId}@sevensitters.com"
    phone: "415 555-000#{sitterProfileId}"

  console.info 'Updating', sitterProfileId, profileData.name
  UserProfile.findOrCreate(id:sitterProfileId).then (userProfile) ->
    userProfile.getUser().then (user) ->
      Q.when(user or User.create()).then (user) ->
        Q.all [
          userProfile.updateAttributes data:profileData, is_simulated:true
          user.setSitterProfile userProfile
          user.updateAttributes userAttributes
          user.getAccounts()
          .then (accounts) ->
            accountAttributes =
              user_id          : user.id
              provider_name    : 'facebook'
              provider_user_id : String(172347878787877 + sitterProfileId)
            accounts.length or Account.create(accountAttributes)
          ]

updateSittersP
  .catch((error) -> console.error error)
  .done ->
    process.exit()
