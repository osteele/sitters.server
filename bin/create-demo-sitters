#!/usr/bin/env coffee

_ = require 'underscore'
Q = require 'q'
Q.longStackSupport = true
_(global).extend require('../app/lib/models')

SitterProfileData = require '../data/sitters.json'

updateSittersP = Q.all SitterProfileData.map (sitterData, i) ->
  n = i + 1
  uuid = sitterData.uuid

  profileData = _.extend {}, sitterData
  delete profileData.uuid

  userAttributes =
    uuid         : uuid
    displayName  : profileData.name
    email        : "sitter-#{n}@sevensitters.com"
    is_simulated : true
    phone        : "415 555-000#{n}"
    role         : 'sitter'

  console.info 'Updating simulated user', profileData.name
  User.findOrCreate({uuid}, userAttributes).then (user) ->
    accountAttributes =
      user_id          : user.id
      provider_name    : 'facebook'
      provider_user_id : String(172347878787877 + n)

    Q.all [
      user.updateAttributes userAttributes

      user.getUserProfile().then (profile) ->
        if profile
          profile.updateAttributes data:profileData
        else
          UserProfile.create(data:profileData).then (profile) ->
            # user.addProfile profile
            profile.setUser user

      # ensure each simulated user has an account, so that the simulator
      # can receive message for that user
      user.getAccounts().then (accounts) ->
        Account.create(accountAttributes) unless accounts.length
    ]

updateSittersP
  .catch((error) -> console.error error)
  .done ->
    process.exit()
