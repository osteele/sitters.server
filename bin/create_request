#!/usr/bin/env coffee

argv = require('optimist').argv
subcommand = argv._[0]

Firebase = require('firebase')
rootFB = new Firebase('https://sevensitters.firebaseIO.com/')
requestFB = rootFB.child('request')

DemoAccountKey = 'facebook/511404287'

RequestTypes =
  'add-sitter': ->
    delay = argv.delay ? 0
    sitterId = argv['sitter-id'] ? 2
    {requestType: 'addSitter', accountKey: DemoAccountKey, parameters: {sitterId, delay}}
  'register-user': ->
    {requestType: 'registerUser', accountKey: DemoAccountKey, parameters: {displayName: 'Sarah', email: 'user@guest.com'}}
  'set-sitter-count': ->
    count = argv.count ? 2
    {requestType: 'setSitterCount', accountKey: DemoAccountKey, parameters: {count}}

if subcommand of RequestTypes
  message = RequestTypes[subcommand]()
  requestFB.push message, -> process.exit()
else
  console.log 'Requests:'
  for name of RequestTypes
    console.log '  ' + name
  process.exit()
