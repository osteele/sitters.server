#!/usr/bin/env coffee

_ = require 'underscore'
argv = require('optimist').argv
util = require 'util'
_(global).extend require('../lib/utils')
_.str = require('underscore.string')
_.mixin(_.str.exports())
require('dotenv').load()

{requestsFB} = require '../lib/firebase'

requestTypeName = argv._[0] # in train-case

TestAccountKey = 'facebook/511404287'

RequestTypes =
  addSitter: -> {delay: 0, sitterId: 2}

  registerDeviceToken: -> {token: '0123456789abcdef0123456789abcdef0123456789abcdef0123456789abcdef'}

  registerUser: -> {displayName: 'Mom', email: 'mom@host.com'}

  reserveSitter: ->
    startTime = new Date().tap -> @.setHours 18
    endTime = new Date().tap -> @.setHours 22
    [startTime, endTime].forEach (date) ->
      date.setMinutes 0
      date.setSeconds 0
      date.setMilliseconds 0
    {delay: 0, sitterId: 2, startTime: startTime.toISOString(), endTime: endTime.toISOString()}

  setSitterCount: -> {count: 2}

mergeCommandLineParameters = (parameters) ->
  for key, value of parameters
    optionName = _(key).underscored()
    optionValue = argv[optionName]
    if optionValue?
      optionValue = Number(optionValue) if value instanceof Number
      parameters[key] = optionValue
  return parameters

requestType = requestTypeName && _(requestTypeName).camelize()
if requestType of RequestTypes
  parameters = RequestTypes[requestType]()
  parameters = mergeCommandLineParameters(parameters)
  request = {
    requestType: requestType
    accountKey: TestAccountKey
    parameters: parameters
  }
  if false
    console.info request
    process.exit()
  requestsFB.push request, -> process.exit()
else
  console.info 'Usage:'
  cmd = argv.$0.replace(/^coffee\s+/, '')
  for key, optionsFn of RequestTypes
    console.info ' ', cmd, _(key).dasherize(), ("--#{_(option).dasherize()}=#{util.inspect(v)}" for option, v of optionsFn()).join(' ')
  process.exit()
