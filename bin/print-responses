#!/usr/bin/env coffee

require('dotenv').load()
_ = require 'underscore'
yaml = require 'js-yaml'
Q = require 'q'

argv = require('optimist').argv
process.env.FIREBASE_ENV = 'production' if argv.production

firebase = require '../app/integrations/firebase'
{MessageFB} = firebase
_(global).extend require('../lib/utils')

UserAuthId = 'facebook-172347878787877'

do ->
  [provider, id] = UserAuthId.split('-', 2)
  firebase.authenticateAs {provider, id}

userMessageFB = MessageFB.child(UserAuthId)

printMessage = (message) ->
  console.log yaml.dump(message)

if argv.follow
  userMessageFB.on 'child_added', (snapshot) ->
    key = snapshot.name()
    message = snapshot.val()
    printMessage message
    userMessageFB.child(key).remove() if argv.remove or argv.delete
else
  userMessageFB.on 'value', (snapshot) ->
    messages = snapshot.val()
    promises = []
    for key, message of messages
      printMessage message
      promises.push firebase.fbRemoveP userMessageFB.child(key) if argv.remove or argv.delete
    unless messages
      console.log "No messages"
    Q.all(promises).done ->
      process.exit()

